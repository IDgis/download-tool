
buildscript {
	repositories {
		jcenter()
		mavenCentral()
	}

	dependencies {
		classpath 'com.bmuschko:gradle-docker-plugin:2.6.5'
	}
}

import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

subprojects {
	
	group = 'nl.idgis.downloadtool'
	
	repositories {
		maven {
			name = "geotoolkit"
			url "http://maven.geotoolkit.org/"
		}
		mavenCentral()
		maven {
			name "idgis-public"
			url "http://nexus.idgis.eu/content/groups/public/"
			credentials {
				username nexusUser 
				password nexusPassword
			}
		}
		maven {
			name "idgis-restricted"
			url "http://nexus.idgis.eu/content/groups/restricted/"
			credentials {
				username nexusUser 
				password nexusPassword
			}
		}
	}
}

// java projects
configure([
	project(':dao'),
	project(':domain'),
	project(':queue'),
	project(':request'),
	project(':feedback'),
	project(':downloader')]) {
	
	apply plugin: 'java'
	
	dependencies {
		compile 'org.slf4j:slf4j-api:1.7.14'
		compile 'org.slf4j:slf4j-log4j12:1.7.14'
		
		testCompile 'junit:junit:4.12'
		testCompile 'org.easymock:easymock:3.4'
	}
}	

// play projects
configure([
	project(':metadata')]) {
	
	apply plugin: 'play'
	apply plugin: 'com.bmuschko.docker-remote-api'
	
	repositories {
		jcenter()
		maven {
			name "typesafe-maven-release"
			url "https://repo.typesafe.com/typesafe/maven-releases"
		}
		ivy {
			name "typesafe-ivy-release"
			url "https://repo.typesafe.com/typesafe/ivy-releases"
			layout "ivy"
		}
	}
	
	model {
		components {
			play {
				platform play: '2.4.6', scala: '2.11', java: '1.8'
				injectedRoutesGenerator = true
			}
		}
		
		tasks {
			copyTar(Copy) {
				dependsOn createPlayBinaryTarDist
				from "${project.buildDir}/distributions/playBinary.tar"
				into "${project.buildDir}/docker"
			}
			
			createDockerfile(Dockerfile) {
				dependsOn copyTar
				destFile = project.file('build/docker/Dockerfile')
				from 'java'
				copyFile "playBinary.tar", '/opt'
				runCommand "cd /opt; tar -xvf playBinary.tar; rm playBinary.tar; chmod u+x playBinary/bin/playBinary"
				exposePort 9000
				defaultCommand "/opt/playBinary/bin/playBinary"
			}
			
			buildImage(DockerBuildImage) {
				dependsOn createDockerfile
				inputDir = project.file('build/docker')
				tag = "${project.name}"
			}
		}
	}
	
}

// application projects
configure([
	project(':request'),
	project(':feedback'),
	project(':downloader')]) {
	
	apply plugin: 'application'
	apply plugin: 'eclipse'
	apply plugin: 'com.bmuschko.docker-remote-api'
	
	dependencies {
		compile project(':dao')
		compile project(':domain')
		compile project(':queue')
	}
	
	task copyTar(type: Copy) {
		dependsOn distTar
		from "${project.buildDir}/distributions/${project.name}.tar"
		into "${project.buildDir}/docker"
	}

	task createDockerfile(type: Dockerfile) {
		dependsOn copyTar
		destFile = project.file('build/docker/Dockerfile')
		from 'java'
		copyFile "${project.name}.tar", '/opt'
		runCommand "cd /opt; tar -xvf ${project.name}.tar; rm ${project.name}.tar; chmod u+x ${project.name}/bin/${project.name}"
		defaultCommand "/opt/${project.name}/bin/${project.name}"
	}

	task buildImage(type: DockerBuildImage) {
		dependsOn createDockerfile
		inputDir = project.file('build/docker')
		tag = "${project.name}"
	}
}

// configure docker plugin
allprojects {
	afterEvaluate { project ->
		if(project.plugins.hasPlugin('com.bmuschko.docker-remote-api')) {
			project.docker {
				url = "http://${dockerHost}:2375"
			}
		}
	}
}

project('metadata') {
	dependencies {
		play project(':domain')
	}
}

project(':dao') {
	dependencies {
		compile project(':domain')
		compile 'nl.idgis.commons:commons-utils:0.0.14'
	
	}
}

project(':queue') {
	dependencies {
		compile project(':domain')
		compile 'com.google.code.gson:gson:2.5'
		compile 'com.dinstone:beanstalkc:2.2.0'
	}
}

project(':request') {
	
	mainClassName = 'nl.idgis.downloadtool.downloadrequest.DownloadRequestController'
}

project(':feedback') {

	dependencies {		
		compile 'nl.idgis.commons:commons-utils:0.0.14'	
	}

	mainClassName = 'nl.idgis.downloadtool.feedback.FeedbackProvider'
}

project(':downloader') {

	dependencies {
		compile 'org.apache.httpcomponents:httpclient:4.3.1'
		compile 'nl.idgis.commons:commons-cache:0.0.14'
		compile 'nl.idgis.commons:commons-convert:0.0.14'
		compile 'nl.idgis.commons:commons-utils:0.0.14'
	}

	mainClassName = 'nl.idgis.downloadtool.downloader.DownloadProcessor'
}

defaultTasks 'clean', 'build', 'buildImage'
