/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'Rob' at '22-3-16 13:57' with Gradle 2.11
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/2.11/userguide/tutorial_java_projects.html
 */
buildscript {
	repositories {
        mavenLocal()
		jcenter()
        mavenCentral()
	}

	dependencies {
		classpath 'com.bmuschko:gradle-docker-plugin:2.6.5'
	}
}

import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

subprojects {
	apply plugin: 'java'
	
	
    group = 'nl.idgis.downloadtool'
//    version = '1.0' // is not used in Tasks yet
    repositories {
	   	mavenLocal()
    	jcenter()
        mavenCentral()
    }
	
	dependencies {
	    compile 'org.slf4j:slf4j-api:1.7.14'
	    compile 'org.slf4j:slf4j-log4j12:1.7.14'
	
    	testCompile 'junit:junit:4.12'
	    testCompile 'org.easymock:easymock:3.4'
	}
	
}

project(':downloadtool-dao') {
    dependencies {
        compile project(':downloadtool-domain')
    }
}

project(':downloadtool-queue') {
    dependencies {
        compile project(':downloadtool-domain')
        compile 'com.google.code.gson:gson:2.5'
        compile 'com.dinstone:beanstalkc:2.2.0'
    }
}

project(':downloadtool-feedback') {
    dependencies {
        compile project(':downloadtool-dao')
        compile project(':downloadtool-domain')
        compile project(':downloadtool-queue')
        
        compile 'com.dinstone:beanstalkc:2.2.0'
    }

}

project(':downloadtool-downloader') {
	apply plugin: 'application'
	apply plugin: 'eclipse'
	apply plugin: 'com.bmuschko.docker-remote-api'

    dependencies {
        compile project(':downloadtool-dao')
        compile project(':downloadtool-domain')
        compile project(':downloadtool-queue')

		compile 'com.dinstone:beanstalkc:2.2.0'
    	compile 'org.apache.httpcomponents:httpclient:4.3.1'
	    
	    compile 'nl.idgis.commons:commons-cache:0.0.15-SNAPSHOT'
	    compile 'nl.idgis.commons:commons-convert:0.0.15-SNAPSHOT'
	    compile 'nl.idgis.commons:commons-utils:0.0.15-SNAPSHOT'
    }
    
    mainClassName = 'nl.idgis.downloadtool.downloader.DownloadProcessor'
    
	task copyTar(type: Copy) {
		dependsOn distTar
		from "${project.buildDir}/distributions/${project.name}.tar"
		into "${project.buildDir}/docker"
	}
	
	task createDockerfile(type: Dockerfile) {
		dependsOn copyTar
		destFile = project.file('build/docker/Dockerfile')
		from 'java'
		copyFile "${project.name}.tar", '/opt'
		runCommand "cd /opt; tar -xvf ${project.name}.tar; rm ${project.name}.tar; chmod u+x ${project.name}/bin/${project.name}"
		defaultCommand "/opt/${project.name}/bin/${project.name}"
	}
	
	task buildImage(type: DockerBuildImage) {
		dependsOn createDockerfile
		inputDir = project.file('build/docker')
		tag = "${project.name}"
	}
}

defaultTasks 'clean', 'build', 'buildImage'
